
add_definitions(-D_CONSOLE -D_AFXDLL)
set(CMAKE_MFC_FLAG 2)

if(CMAKE_CXX_FLAGS MATCHES "/EHs")
    string(REGEX REPLACE "/EHs" "/EHa" CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
else()
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /EHa")
endif()

macro(add_meazure_test runner reuse)
    add_executable(${runner} WIN32 ${runner}.cpp ${ARGN})
    if("${reuse}" STREQUAL "")
        target_precompile_headers(${runner} PRIVATE pch.h)
    else()
        target_precompile_headers(${runner} REUSE_FROM ${reuse})
    endif()
    target_include_directories(${runner} PRIVATE ${APP_DIR} ${Boost_INCLUDE_DIRS})
    target_link_libraries(${runner} ${Boost_UNIT_TEST_FRAMEWORK_LIBRARY})
    add_test(${runner} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${runner})    
endmacro()

add_meazure_test(ColorsTest "" ${APP_DIR}/Colors.cpp)
add_meazure_test(GUIDTest ColorsTest ${APP_DIR}/GUID.cpp)
add_meazure_test(TimeStampTest ColorsTest ${APP_DIR}/TimeStamp.cpp)
add_meazure_test(UnitsTest ColorsTest ${APP_DIR}/Units.cpp)
add_meazure_test(UtilsTest ColorsTest ${APP_DIR}/Utils.cpp)
